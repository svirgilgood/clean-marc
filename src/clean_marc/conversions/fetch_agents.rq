PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX bflc: <http://id.loc.gov/ontologies/bflc/>
PREFIX cc: <http://creativecommons.org/ns#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX madsrdf: <http://www.loc.gov/mads/rdf/v1#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xs: <http://www.w3.org/2001/XMLSchema#>


SELECT
  (GROUP_CONCAT(DISTINCT str(?agentIRI); separator=";") AS ?ari)
  (GROUP_CONCAT(DISTINCT ?l; separator=";") AS ?label)
  (GROUP_CONCAT(DISTINCT ?mf; separator=";") AS ?marcfield)
  (GROUP_CONCAT(DISTINCT ?fi; separator=";") AS ?firstIndicator)
  (GROUP_CONCAT(DISTINCT ?si; separator=";") AS ?secondIndicator)
  ?marcKey
WHERE {
  ?work
    (bf:contribution/bf:agent)|bf:subject ?agentIRI .
  ?agentIRI
    a bf:Agent ;
    bflc:marcKey ?marcKey ;
    rdfs:label ?l .
  BIND(
    SUBSTR(?marcKey, 1, 3)
    AS ?mf
  )
  BIND(
    SUBSTR(?marcKey, 4, 1)
    AS ?fi
  )
  BIND(
    SUBSTR(?marcKey, 5, 1)
    AS ?si
  )
}
GROUP BY ?marcKey
ORDER BY ?label

