PREFIX xs: <http://www.w3.org/2001/XMLSchema#>
PREFIX cc: <http://creativecommons.org/ns#>
PREFIX madsrdf: <http://www.loc.gov/mads/rdf/v1#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX bflc: <http://id.loc.gov/ontologies/bflc/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

SELECT
  (?prefLabel AS ?altLabel)
  ?prefLabel
  (GROUP_CONCAT(str(?place)) AS ?pps)
  (GROUP_CONCAT(DISTINCT str(?subject); separator=";") AS ?subjectFor)
  (GROUP_CONCAT(DISTINCT str(?pubLocation); separator=";") AS ?publocationsFor)
  (GROUP_CONCAT(DISTINCT str(?externalID); separator="; ") AS ?externalIDs)
WHERE {
  {
    VALUES ?labelPred {
      madsrdf:authoritativeLabel
      rdfs:label
    }
    ?pubLocation
      (bf:capture/bf:place)|bf:originPlace ?place ;
    .

    ?place
      a bf:Place ;
      ?labelPred ?prefLabel ;
      .

  } UNION {
    ?pubLocation
      bf:provisionActivity ?provActivity ;
    .
    ?provActivity
      bflc:simplePlace|bflc:place ?prefLabel ;
    .

  } UNION {
    ?subject
      bf:subject ?topic ;
    .

    ?topic
      madsrdf:componentList/(rdf:rest*|rdf:first)* ?topicTerm ;
    .

    ?topicTerm
      a madsrdf:Geographic ;
      madsrdf:authoritativeLabel|rdfs:value ?prefLabel ;
    .

    FILTER NOT EXISTS {
      ?topic
        bf:source ?externalSchemas ;
      .
      VALUES ?externalSchemas {
        <http://id.loc.gov/vocabulary/subjectSchemes/gnd>
        <http://id.loc.gov/vocabulary/subjectSchemes/rvm>
      }
    }
    FILTER NOT EXISTS {
      ?topicTerm
        a bf:Hub ;
        .
    }

  }
}
GROUP BY ?prefLabel
ORDER BY ?prefLabel

